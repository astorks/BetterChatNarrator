import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

plugins {
    id 'net.fabricmc.fabric-loom-remap' version "${loom_version}"
    id 'maven-publish'
    id 'org.jetbrains.kotlin.jvm' version '2.3.0'
    id 'org.jetbrains.kotlin.plugin.serialization' version '2.3.0'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

version = project.mod_version
group = project.maven_group

base {
    archivesName = project.archives_base_name
}

repositories {
    // Add repositories to retrieve artifacts from in here.
    // You should only use this when depending on other mods because
    // Loom adds the essential maven repositories to download Minecraft and libraries from automatically.
    // See https://docs.gradle.org/current/userguide/declaring_repositories.html
    // for more information about repositories.

    maven { url = "https://maven.terraformersmc.com/releases" }
    maven { url = 'https://maven.wispforest.io' }
    maven { url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
    maven { url = "https://repo.aikar.co/content/groups/aikar/" }
    maven { url = 'https://repo.extendedclip.com/releases/' }
    maven { url = 'https://repo.papermc.io/repository/maven-public/' }
    mavenCentral()
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_api_version}"
    modImplementation "net.fabricmc:fabric-language-kotlin:${project.fabric_kotlin_version}"

    modImplementation "io.wispforest:owo-lib:${project.owo_version}"
    include "io.wispforest:owo-sentinel:${project.owo_version}"
    annotationProcessor modImplementation("io.wispforest:owo-lib:${project.owo_version}")
    modImplementation "com.terraformersmc:modmenu:17.0.0-beta.1"

    // Mod Library Dependencies
    include 'org.yaml:snakeyaml:2.5'
    implementation 'org.yaml:snakeyaml:2.5'

    include 'com.microsoft.cognitiveservices.speech:client-sdk:1.47.0@jar'
    implementation 'com.microsoft.cognitiveservices.speech:client-sdk:1.47.0@jar'

    shadow "org.jetbrains.kotlin:kotlin-stdlib:2.3.0"
    shadow "org.jetbrains.kotlin:kotlin-reflect:2.3.0"

    // Velocity Plugin
    compileOnly("com.velocitypowered:velocity-api:3.1.1")
    annotationProcessor("com.velocitypowered:velocity-api:3.1.1")
    shadow "co.aikar:acf-velocity:0.5.1-SNAPSHOT"

    // Spigot Plugin Dependencies
    compileOnly 'org.jetbrains.kotlin:kotlin-stdlib:2.3.0'
    compileOnly 'org.spigotmc:spigot-api:1.21.11-R0.1-SNAPSHOT'
    compileOnly 'me.clip:placeholderapi:2.11.7'
    shadow "co.aikar:acf-paper:0.5.1-SNAPSHOT"

    shadow "com.charleskorn.kaml:kaml:0.56.0"
}

processResources {
    inputs.property "version", project.version
    filteringCharset "UTF-8"

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }

    filesMatching("plugin.yml") {
        expand "version": project.version
    }

    filesMatching("velocity-plugin.json") {
        expand "version": project.version
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.release = 21
}

tasks.withType(KotlinCompile).configureEach {
    kotlinOptions {
        jvmTarget = 21
        javaParameters = true
    }
}

java {
    withSourcesJar()

    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

jar {
    inputs.property "archivesName", project.base.archivesName

    from("LICENSE") {
        rename { "${it}_${inputs.properties.archivesName}"}
    }
}

shadowJar {
    configurations = [project.configurations.shadow]
    archiveClassifier.set("dev")

    relocate 'kotlin', 'mc.betterchatnarrator.kotlin'
    relocate 'org.intellij', 'mc.betterchatnarrator.intellij'
    relocate 'org.jetbrains', 'mc.betterchatnarrator.jetbrains'

    relocate 'co.aikar.commands', 'mc.betterchatnarrator.acf'
    relocate 'co.aikar.locales', 'mc.betterchatnarrator.acf.locales'
}

remapJar {
    dependsOn(shadowJar)
    inputFile = tasks.shadowJar.archiveFile
}

// configure the maven publication
publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }

    // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
    repositories {
        // Add repositories to publish to here.
        // Notice: This block does NOT have the same function as the block in the top level.
        // The repositories here will be used for publishing your artifact, not for
        // retrieving dependencies.
    }
}
